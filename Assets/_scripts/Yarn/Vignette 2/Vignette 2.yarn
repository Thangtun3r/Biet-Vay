
title: setup
---

<<declare $horseToken = -1>>
<<declare $currentBalance = 1600000>>
<<declare $currentHolding = 0>>
<<declare $betPlaced = false>>
<<declare $horseNames = "snicker">>

<<declare $payOutDone = false>>

///bool for betting result
<<declare $winningBet = false>>
<<declare $raceCompleted = false>>

===

title: introduce
---
<<killFMODEvent>>
<<playFMODEvent event:/Vignette 2/On the train home>>
<<UnlockMouse>>
===
title: backToPrevious
---
<<disablePlayer>>
<<expandIntro>>
===





title: Exit
---
<<if $payOutDone == true >>
<<transition 1>>
<<jump RestartTheLoop>>
<<else>>
<<collapse>>
Liem: I haven't bet on anything yet.
Liem: I should go place a bet first.
<<expand>>
<<endif>>
===

title: RestartTheLoop
---
<<spawn start>>
<<newBettingDay>>
<<set $payOutDone = false>>
<<set $winningBet = false>>
<<set $raceCompleted = false>>
<<set $betPlaced = false>>
<<set $currentBalance = $currentBalance + $currentHolding>>
<<set $currentHolding = 0>>

<<collapse>>
Liem: Another day, another chance to win big.
Liem: Let's see if I can turn my luck around today.
<<jump ATM>>
===


title: start
---
<<isNotBietVay>>
<<collapse>>
<<disablePlayer>>
-> [bv]Biet vay [/bv]/I/[sw w="shouldn't have"]had[/sw]/bet/on/that/horse.   
    <<spawn start>>
    <<killFMODEvent>>
    <<isBietVay>>
    <<transition 1>>
    <<PsudoTurnOff>>
    <<jump WifePhoneCall>>
-> I/had/bet/on/that/horse.
    <<resolve>>

===




title: BettingCorner
---

<<VMBoost 2>>
<<collapse>>


Receptionist: Welcome to NRA off-course betting center.
Receptionist: How can I help you, sir?
<<if $raceCompleted == false && $betPlaced == false && $payOutDone == false>>
<<jump placeBet>>>>
<<elseif $raceCompleted == true && $betPlaced == true && $payOutDone == false>>
<<jump Payout>>
<<else>>
-> Never mind./
    Receptionist: Alright, have a nice day sir.
<<endif>>


  
<<VMReset>>
<<expand>>

===


title: placeBet
---
-> I /would like/ to /place a bet./
    <<if $currentHolding > 0>>
        <<jump PlaceBet>>
    <<else>>
        Receptionist: What’s with the empty hand??
        Receptionist: You need money to place a bet.
        Receptionist: If you don’t have any money, don’t think about betting at all.
    <<endif>>
-> How/should/I/place a bet./
    Liem: So how should I bet?
    Receptionist: You can place a bet on any horse you like. The lower the odds, the lower the risk.
    Receptionist: However, the payout will also be lower.
    Receptionist: The higher the odds, the higher the risk.
    Receptionist: But remember 99.8% of gamblers quit before winning big.
    Receptionist: So never give up!
-> Never mind./
    Receptionist: Alright, have a nice day, sir.
<<VMReset>>
<<expand>>
===

title: Payout
---

-> I/would like/to/redeem/my ticket./
<<RedeemSlip BettingSlipWrapper>>
Liem: I would like to redeem my ticket.
Receptionist: Okay, let me check your ticket.
<<payOut>>
    <<if $winningBet == false>>
    Receptionist: I’m sorry, sir.
    Receptionist: This ticket didn't win anything.
    Receptionist: Better luck next time.
    <<transition 2>>
    <<expand>>        
    <<restartScene>>
    <<else>>
    Receptionist: Congrats, sir.
    Receptionist: You have won {$currentHolding} VND.
    Receptionist: Here is your payout with your stake and your winnings combined.
    <<transition 2>>
    <<VMReset>>
    <<jump RestartTheLoop>>
    <<endif>>
<<set $payOutDone = true>>
<<VMReset>>
<<expand>>
===

title: PlaceBet
---
Receptionist: Certainly, which horse would you like to bet on?
    -> Typical Day
        <<placeBet 0>>
        <<set $horseNames = "Typical Day">>
        <<jump ConfirmBet>>
    -> Super Freak
        <<placeBet 1>>
        <<set $horseNames = "Super Freak">>
        <<jump ConfirmBet>>
    -> Uguri Hat
        <<placeBet 2>>
        <<set $horseNames = "Uguri Hat">>
        <<jump ConfirmBet>>
    -> Loud Suzuki
        <<placeBet 3>>
        <<set $horseNames = "Loud Suzuki">>
        <<jump ConfirmBet>>
    -> Phi Cuong
        <<placeBet 4>>
        <<set $horseNames = "Phi Cuong">>
        <<jump ConfirmBet>>
    -> Skibidi Huzz
        <<placeBet 5>>
        <<set $horseNames = "Skibidi Huzz">>
        <<jump ConfirmBet>>
    -> Never mind./
        Receptionist: Alright, if you have any questions, feel free to ask.
<<VMReset>>
<<expand>>

===

title: ConfirmBet
---
    Receptionist: Right, got it.
    <<playFMOD1shot event:/Vignette 2/KeyboardTyping>>
    Receptionist: Let me re-confirm: you would like to go all-in on {$horseNames}, correct?
    -> Yes.
        Receptionist: Ok sir, let me process your bet.
        <<playFMOD1shot event:/Vignette 2/KeyboardTyping_2>>
        <<wait  1.3>>
        <<playFMOD1shot event:/Vignette 2/PrintSlip>>
        <<wait  2.8>>
        Receptionist: Alright, your bet has been placed.
        Receptionist: Please take the printed ticket to later claim your payout.
        Receptionist: After the race finishes, you can come back here to redeem your ticket.
        Receptionist: Good luck, sir.
        <<set $betPlaced = true>>
        <<VMReset>>
        <<expand>>
        <<jump BettingSlip>>

===

title: BettingSlip
---
<<transition 1>>
<<enableSlip BettingSlipWrapper>>
<<spawn race>>
<<jump RaceStart>>
===


title: RaceStart
---
<<expand>>
<<playFMOD1shot event:/Vignette 2/Horn>>
<<wait  4>>
<<startRace>>
<<wait  2.4>>
<<playFMOD1shot event:/Vignette 2/Horse running>>
===



title: WifePhoneCall
---

<<collapse>>
<<playFMODEvent event:/Vignette 2/Phone Ring>>
Phone: (Phone ringing)
-> *Pick up*
    <<playFMODEvent event:/Vignette 2/phone pickup>>
    <<jump PickUpPhone>>
-> *Hang up*
    <<playFMODEvent event:/Vignette 2/phone hangup>>
    <<jump ATM>>
===


title: PickUpPhone
---
Disturbing Woman: ...
Disturbing Woman: Alo?
-> What/ do/ you/ want?
Disturbing Woman: Why haven’t you answered my call?
Disturbing Woman: Have you paid for Tam’s tuition yet? His teacher has been calling me saying we’re late with the fee.
Disturbing Woman: Please don’t tell me you’re going to the casino again.
-> No,/I'm/working/for/the money.
Liem: No, I am working for the money.
Liem: I’ll triple it—
Liem: I mean, I promise I’ll pay for Tam and also sign him up for his extra math class as well.
Liem: And pay the remaining debt for the house.
Disturbing Woman: Please, I'm begging you. It’s our son’s future—
<<playFMODEvent event:/Vignette 2/phone hangup>>
<<jump ATM>>

===

title: ATM
---

<<VMBoost 1>>
<<collapse>>
-> *Insert/card* 
    <<playFMODEvent event:/Vignette 2/Atm card insert>>
    <<cardInsert>>
    -> *Deposit*
        <<playFMODEvent event:/Vignette 2/Atm button>>
        <<set $currentBalance = $currentHolding + $currentBalance>>
        <<set $currentHolding = 0>>
        <<deposit>>
    -> *Withdraw*
        <<playFMODEvent event:/Vignette 2/Atm button>>
        <<playFMODEvent event:/Vignette 2/Atm money>>
        <<set $currentHolding = $currentHolding + $currentBalance>>
        <<set $currentBalance = 0>>
        <<withdraw>>
-> No.        
<<VMReset>>
<<expand>>
===

title: Money
---
<<set $currentHolding = $currentHolding + 56000000>>
===

title: Debug
---

<<payOut>>

===