//Declare variables
//// To-do describe the purpose through dialogue


// Tam archytupe:
//Like throwing bad pun

// Mai archytype
// Act like she optimistic about things
// Very active

title: PauseMenu
---
<<collapse>>
->Main Menu
    <<expand>>
->To Desktop
    <<expand>>


===

title: BietVay
---
<<transition 2>>
<<expand>>        
<<restartScene>>

===

title: Setup
---
<<declare $affection = 0>>
<<declare $favoriteGashapon = false>>
<<declare $movieFlirtSuccess = false>>
<<declare $laterRejection = false>>


===
title: introduce
---
<<UnlockMouse>>
===
title: backToPrevious
---
<<disablePlayer>>
<<expandIntro>>
===



title: start
---
<<isNotBietVay>>
<<collapse>>
<<disablePlayer>>
<<PsudoTurnOn>>
-> [bv]Biet vay,[/bv]/I/ [sw w="shouldn't have"] have [/sw]/confessed/to/her.
    <<transition 1>>
    <<isBietVay>>
    <<PsudoTurnOff>>
    <<spawn Home>>
    <<jump Home>>
-> I/have/confessed/to/her.
    <<resolve>>
-> Debug
    <<transition 1>>
    <<isBietVay>>
    <<PsudoTurnOff>>
    <<transition 5>>
    <<expand>>
    <<VMReset>>
    <<jump Convenience_store>>
===

title: Home
---
<<expand>>


===


title: morningCall
position: -725,100
---
<<collapse>>
<<once>>
Mai: <shake a=0.5>Heyyy...</shake>
Mai: On your way yet?  
Mai: Looks like it’s gonna <shake a=0.5>rain!</shake>
-> I/ am/ on/ my/ way.
Tam: I’m on my way.  
Mai: Okay, hurry up or you’ll get drenched lol.  
<<else>>
Game: What are you doing?  
Game: Get your ass ready and <shake a=0.5>go.</shake>
<<endonce>>
<<expand>>
===

title: toPark
position: -356,48
---
<<collapse>>
Game: Ready to go?  
-> Yes,/ let's/ go.
    <<transition 2>>
    <<spawn Mall>>
    <<expand>>
-> No
    Game: Aight, bet.  
    <<expand>>
===


////Park part

title: park
position: -336,-191
---
<<VMBoost 5>>
<<collapse>>
-> Did/I/keep/you/waiting?
Tam: Did I keep you waiting?
Mai: Not really. Just arrived here for a bit.
Mai: Ah before heading to the park. Wanna eat some durian cheesecake?
Mai: Freshly baked by my mom.
-> Looks/so yummy!
Tam: Looks so yummy!
Tam: And it smells amazing!
Mai: I know, right?  
Mai: Let's go inside and enjoy it.
->Yeh/let's/go.
Tam: Yeh, let’s go.
    <<transition 2>>
    <<VMReset>>
    <<jump insidePark>>
===



title: insidePark
---
<<freeze>>
<<spawn Bench>>
<<expand>>

===

///park chitchat

title: ParkChitchat
---
<<VMBoost 1>>
<<collapse>>
<<once>>

-> The cheesecake/ was/ very /delicious!
    Tam: The cheesecake was very delicious!
    Tam: Your mom still overdoes the vanilla… in a good way.
    Mai: She says it tastes better when it’s too much haha.
    -> So.../ how was/ high school?
        Tam: So… how was high school?
        Mai: It was fun!... there were a bunch of things I could do.
        Mai: And hehe, let’s just say… my eyes are <wiggle a=0.5>very</wiggle> happy there!
        Tam: Oh really? What do you mean?
        Mai: Our school basketball team was full of handsome! Like unfairly handsome.
        -> Oh/ok.
            Tam: Oh ok.
        -> The hell.
            Tam: The h-
            Mai: Huh?
            Tam: Ah- I mean cool cool.
        Mai: And he-he, you wouldn't guess which club I got into.
        -> Let/me/guess.../Basketball/club?
            Tam: Let me guess... Basketball club?
                Mai: Nooo, it's <wiggle a=0.5>Art Club!</wiggle>
                -> Drawing?/ You?
                    Tam: Drawing? You?
                    Tam: …You always joked you weren’t great at it.
                    Mai: Hey! I’ve been practicing!
                    <<set $affection -= 1>>  // Dismissive
                -> You?/ Drawing?
                    Tam: You? Drawing?
                    Tam: …You always joked you weren’t great at it.
                    Mai: Hey! I’ve been practicing!
                    <<set $affection -= 1>>  // Dismissive
                -> Woah,/ that's/ awesome!
                    Tam: Woah, that’s awesome!
                    Tam: Honestly, I’ll retire the stick-onomy joke. For tonight.
                    Mai: Good. You better be!
                    <<set $affection += 1>>  // Supportive
        -> Basketball/club?
            Tam: Basketball club?
                Mai: Nooo, it's <wiggle a=0.5>Art Club!</wiggle>
                -> Drawing?/ You?
                    Tam: Drawing? You?
                    Tam: …You always joked you weren’t great at it.
                    Mai: Hey! I’ve been practicing!
                    <<set $affection -= 1>>  // Dismissive
                -> You?/ Drawing?
                    Tam: You? Drawing?
                    Tam: …You always joked you weren’t great at it.
                    Mai: Hey! I’ve been practicing!
                    <<set $affection -= 1>>  // Dismissive
                -> Woah,/ that's/ awesome!
                    Tam: Woah, that’s awesome!
                    Tam: Honestly, I’ll retire the stick-onomy joke. For tonight.
                    Mai: Good. You better be!
                    <<set $affection += 1>>  // Supportive

Tam: You gotta draw me someday, yeah?
Tam: I promise I’ll pay… like, friend discount rates.
Mai: Haha, sure! I’ll hold you to that. Best friend discount it is.

Tam: Oh and how about your studying, actually?
Mai: Lot of assignments! Everyone’s so good… I’m not sure I can keep up.
    -> You/ are/ doing great./
        Tam: I can tell you’re doing great already.
        Mai: Thanks, Tam.
        <<set $affection += 1>>      // Supportive
    -> Just.../chill out.
        Tam: Well, just chill out. Don’t stress too much.
        Mai: I don't know. Like in several years we must get a job.
        Mai: And we cannot be silly with our problems anymore.
        Tam: Cuz funny accident become money accident right?
        Mai: Tam. No more pun.
        // +0 (neutral)
    -> Wanna/ study together/ sometime?
        Tam: Wanna study together sometime?
        Mai: You? Not just the night before the deadline ?!?!
        Tam: People change, ya know.
        Mai: Okay, let's see how it goes.
        <<set $affection += 1>>      // Proactive help

<<transition 2>>
<<VMReset>>
<<weather>>
<<jump insidePark>>
<<endonce>>



-> Kinda/chill/isn't/it?
Mai: Yeh, very chill indeed.
Mai: It's been so long since I've been this relaxed.
Mai: Hey, It's evening already, you got any plans?

        -> Let's/ go/to/the gashapen!
            Tam: How about we go to play some gashapon? 
            Mai: Okay. That sounds fun!
            Tam: Let's hit the Mall!
            <<transition 2>>
            <<VMReset>>
            <<jump Gashapon>>
===

title: Gashapon
---
<<expand>>
<<spawn gashapon>>
<<unfreeze>>

<<isBietVay>>

===


title: MaiAtGashapon
---
<<VMBoost 2>>
<<collapse>>
<<once>>
Mai: Hey look! Let's try out this one!
Mai: It’s the gacha with cats and ID cards, something like that.
Mai: Superrr cool, they even made a parody card of Severance.
Tam: Really!?! Well we better get the special one.
-> Aight,/watch/this.
Tam: Aight, watch this.
Mai: Ok, the stage is all yours.
<<else>>
Mai: What are you waiting for? Show me your SSR luck!
<<endonce>>
<<VMReset>>
<<expand>>
===



title: GachaMachineGame
---
<<VMBoost 3>>
<<collapse>>
-> *Turn/ the crank*
    <<roll>>
    <<wait 3>>
    <<if $favoriteGashapon>>
    Mai: Oh it's McLovin parody cat!
    Mai: It's the limited oneee.
    ->Well/ you/ can/ have it./
    Tam: Well you can have it.
    Mai: Really? Thanks alot!
    Tam: Yep yep.
    <<set $affection += 1>>
    <<else>>
    Mai: Oh it's just a regular one.
    Mai: Still cute tho.
    Mai: You should put it in your wallet!
    Mai: To disguise as a business card.   
    -> Haha,/ sure.
    Tam: Haha, sure.
    <<endif>>
    <<jump Convenience_store_transition>>
        
                

===




title: Convenience_store_transition
---
Mai: Well, that was fun but expensive as hell.
    -> Yeh,/but/worth it/,right?
    Tam: Yeh, but worth it, right?
    Mai: Yeh maybe haha.
Mai: Looks like it’s going to rain soon. We should head home.  
Tam: Ah, wait.  
-> How about/ we/ swing/ by /the convenience store/
Tam: How about we swing by the convenience store to wait out the rain?

<<if $affection >= 2 >>
    Mai: Oh, that’s a good idea.  
    Mai: I need to buy some stuff anyway.  
    -> Alright/ let’s/ go then.
        Tam: Alright let's go then.
        <<transition 5>>
        <<expand>>
        <<VMReset>>
        <<jump Convenience_store>>


<<else>>
    <<jump suddenGoHome>>

<<endif>>
===


title: Convenience_store
---
<<isBietVay>>
<<PsudoTurnOff>>
<<expand>>
<<freeze>>
<<spawn ConvenienceStore>>
    
===

title: Convenince_store_mai
---
<<freeze>>
<<VMBoost 4>>
<<collapse>>
-> Lucky/ we/ stopped/ here.
    Tam: Lucky we stopped here.
Mai: Ya, if we’d gone earlier, we’d be soaked.
Mai: So...real talk! Are you interested in anyone in your class?
-> Not/really.
    Tam: Not really.
    Tam: My class is all male, so… yeah.
Mai: I can help you find someone! Let me be your wingman.
Mai: And maybe you can help me find someone too.
-> Nah,/ I’m/ good.
    Tam: Nah, I’m good.
Mai: Aww, come on. I could introduce you to Linh she’s really nice.
Mai: Or Thảo from my club. She loves games!
-> What/about/ Mai?
    Tam: What about Mai?
    Mai: Mine? 
    Mai: You mean you have a girlfriend already?
    Mai: Aww, that's shucks.
    ->  [fr]I like you.[/fr]
        Tam: No, I mean Mai. You.
        Tam: I like you.
        Mai: Whoa—hold up. This is very sudden.
        -> I’ve/ had/ a crush/on you/ since/ high school.
            Tam: I’ve had a crush on you since high school.
            Mai: ...
            Mai: But I thought we were just friends.
            Mai: Tam, I truly appreciate your feelings.
<<if $affection >= 3>>
    Mai: It’s sudden… Let me think about it, okay? I’ll text you.
    Mai: Today was fun. Thanks for hanging out with me.
        <<transition 2>>
        <<VMReset>>
        <<jump delayedRejection>>
        

<<elseif $affection >= 2>>
    Mai: And I truly value our friendship.
    Mai: And I don't want to break it.
    Mai: So...I hope we can still be friends. Still friends?
    -> Sure./ I/ understand.
        Tam: Sure, I understand.
        <<transition 2>>
        <<expand>>        
        <<restartScene>>
    
<<else>>
    <<jump suddenGoHome>>
<<endif>>
===

title: suddenGoHome
---
Mai: I have to go home now.
Tam: What? Why?
Mai: Sorry, Tam. My mom texted—she needs my help.
Mai: Maybe we can hang out another time?
-> Sure,/ I/ understand.
    Tam: Sure, I understand.
    <<jump BietVay>>

===

title: delayedRejection
---
<<spawn Home>>
<<collapse>>
Phone: (Notification)
Mai: ...
Mai: Hey Tam, after thinking a lot about it—
Mai: I truly value our time together and appreciate your feelings.
Mai: But I don’t think I’m ready to go further than friendship.
Mai: We just hung out again after a long time, and we don’t know how much we’ve changed.
Mai: So I don’t think we should rush into a relationship.
Mai: I hope you understand, Tam.
Mai: So… still friends?
-> Yeah,/ still/ friends.
    Tam: Yeah, still friends.
        <<jump BietVay>>

===

